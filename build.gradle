import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'dropwizard-demo'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    // https://mvnrepository.com/artifact/com.hubspot.dropwizard/dropwizard-guice
    compile group: 'com.hubspot.dropwizard', name: 'dropwizard-guice', version: '0.7.0.2'
    // https://mvnrepository.com/artifact/com.google.inject/guice-parent
    compile group: 'com.google.inject', name: 'guice-parent', version: '3.0', ext: 'pom'
    // https://mvnrepository.com/artifact/org.mongodb/mongo-java-driver
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '2.10.1'
    // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

mainClassName = 'com.demo.restapi.RestAPIApp'

run {
    args = ['server', 'config.yml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'dropwizard-demo.jar'
}

task stage(dependsOn: ['build', 'clean', 'shadowJar'])
build.mustRunAfter clean